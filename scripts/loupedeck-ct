#!perl
use strict;
use 5.020;
use experimental 'signatures';

use Getopt::Long;
use Pod::Usage;
use PerlX::Maybe;

use Imager;

use HID::LoupedeckCT;

=head1 SYNOPSIS

    loupedeck-ct --brightness 0

=head1 OPTIONS

=over 4

=item C<--uri>

The URI to connect to

=item C<--brightness>

Set the current display brightness

=item C<--reset>

Reset the device

=item C<--persist-brightness>

Set the brightness that will be restored when plugging the device in

=item C<--vibrate>

Vibrate in the pattern

Pattern numbers range from 10 to 60.

=back

=cut

GetOptions(
    'uri=s' => \my $uri,
    'brightness=i' => \my $brightness,
    'info' => \my $info,
    'reset' => \my $reset,
    'persist-brightness' => \my $persist_brightness,
    'vibrate:i' => \my $vibration_pattern,
) or pod2usage(2);

if( defined $vibration_pattern ) {
    $vibration_pattern ||= 10;
};

my $ld = HID::LoupedeckCT->new(
    maybe uri => $uri,
);

my $command = $ld->connect->then(sub {
    if( defined $reset ) {
        return $ld->reset;
    } else {
        return Future->done;
    };
})->catch(sub {
        say "Error:";
        say $_ for @_;
})->then(sub {
    if( defined $brightness ) {
        return $ld->set_backlight_level($brightness, persist => $persist_brightness);
    } else {
        return Future->done;
    };

})->then(sub {
    if( defined $vibration_pattern ) {
        return $ld->vibrate( $vibration_pattern );
    } else {
        return Future->done;
    };

})->then(sub {
    if( defined $info ) {
        return $ld->get_firmware_version()->then(sub(%v) {
            say "Firmware version: $_=$v{$_}"
                for sort keys %v;
            return Future->done;
        })->then(sub {
            return $ld->get_serial_number()
        })->then(sub( $v ) {
            say "Serial number: $v";
            Future->done;
        })->then(sub {
            return $ld->get_mcu_id()
        })->then(sub( $v ) {
            say "MCU id : $v";
            Future->done;
        })->catch(sub {
        say "Error:";
        say $_ for @_;
        });

    } else {
        return Future->done
    };

})->then(sub {
    Mojo::IOLoop->stop;
    Future->done;
});

Mojo::IOLoop->start unless Mojo::IOLoop->is_running;
